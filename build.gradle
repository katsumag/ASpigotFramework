import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.0-rc'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
}

group = 'me.katsumag'
version = '1.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext.kotlin_version = '1.4.0-rc'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.katsumag.me/repository/maven-releases' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.16.1-R0.1-SNAPSHOT'
    implementation "me.katsumag:item-actions-lib:2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

shadowJar {
    relocate 'kotlin', 'me.katsumag.aspigotframework.shaded.kotlin'
    relocate 'me.katsumag.itemactionslib', 'me.katsumag.aspigotframework.shaded.ial'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'me.katsumag'
            artifactId = 'a-spigot-framework'
            version = '1.0'
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://repo.katsumag.me/repository/maven-releases"
            def snapshotsRepoUrl = "https://repo.katsumag.me/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username System.env["MAVEN_USER"]
                password System.env["MAVEN_PASSWORD"]
            }
        }
    }
}